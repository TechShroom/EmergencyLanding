buildscript {
    repositories {
        mavenCentral()
        maven {
            name "central snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name "gradle plugins"
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies {
        classpath group: 'gradle.plugin.nl.javadude.gradle.plugins', name: 'license-gradle-plugin', version: '0.12.0'
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.0'
    }
}
apply plugin: 'aversion-apt'
apply plugin: 'aversion-maven'
apply plugin: 'aversion-util'
apply plugin: 'idea'

util {
    javaVersion = '1.8'
}
idea.project.languageLevel = util.javaVersion

mavencfg {
    description 'A game engine created for TechShroom Studios'
    coords 'TechShroom', 'EmergencyLanding'
}

apply plugin: 'com.github.hierynomus.license'

defaultTasks 'clean', 'licenseFormat', 'build'

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

repositories {
    mavenCentral()
    maven {
        name "central snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

ext.lwjglVersion = '3.0.1-SNAPSHOT'

def addLWJGL(ext, natives) {
    // "lwjgl" or "lwjgl-ext"
    def lwjgl = "lwjgl" + (ext ? "-" + ext : "")
    project.dependencies {
        compile group: 'org.lwjgl', name: lwjgl, version: project.lwjglVersion
        if (natives) {
            compileOnly group: 'org.lwjgl', name: lwjgl, version: project.lwjglVersion, classifier: 'natives-macos'
            compileOnly group: 'org.lwjgl', name: lwjgl, version: project.lwjglVersion, classifier: 'natives-linux'
            compileOnly group: 'org.lwjgl', name: lwjgl, version: project.lwjglVersion, classifier: 'natives-windows'
        }
    }
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.0.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.0.2'
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compile group: 'com.flowpowered', name: 'flow-math', version: '1.0.1'

    // LWJGL
    addLWJGL('', true);
    addLWJGL('opengl', false);
    addLWJGL('openal', true);
    addLWJGL('glfw', true);
    addLWJGL('nanovg', true);
    addLWJGL('stb', true);
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.2'
    compile group: 'io.netty', name: 'netty-all', version: '4.0.36.Final'
    compile group: 'com.apple', name: 'AppleJavaExtensions', version: '1.4'
    compile group: 'net.engio', name: 'mbassador', version: '1.2.4.2'
    compile group: 'org.l33tlabs.twl', name: 'pngdecoder', version: '1.0'
    compile group: 'com.squareup', name: 'javapoet', version: '1.7.0'
    compile group: 'org.jdeferred', name: 'jdeferred-core', version: '1.2.4'
    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '19.0'
    addAPT group: 'com.google.auto.factory', name: 'auto-factory', version: '1.0-SNAPSHOT'
    addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-SNAPSHOT'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.3'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets.main {
    java.srcDir 'src/generated/java'
    resources.srcDir 'src/generated/resources'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    exclude '**/Sync.java'
    exclude '**/Color.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task detokenizeSource(type: Copy) {
    from sourceSets.main.allJava.srcDirs
    into 'build/detokenizedSource'
    inputs.property 'version', project.version.toString()
    filter(ReplaceTokens, tokens: [
        'NAME': project.name.toString(),
        'VERSION': project.version.toString()
    ])
}

compileJava.dependsOn(detokenizeSource)
compileJava.source = fileTree(dir: 'build/detokenizedSource')
